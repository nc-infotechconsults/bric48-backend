import java.text.SimpleDateFormat
import java.util.Date

plugins {
	id 'java'
  	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.liquibase.gradle' version '2.2.2'
	id 'org.hibernate.orm' version '6.5.3.Final'
}

group = 'it.infotechconsults.bric48'
version = '1.0.0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
	mavenCentral()
}

dependencies {
	// PostgreSQL Driver
	implementation 'org.postgresql:postgresql:42.7.4'

	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.liquibase:liquibase-core'

	compileOnly 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // JWT Library
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6', 'io.jsonwebtoken:jjwt-jackson:0.12.6'

	liquibaseRuntime 'info.picocli:picocli:4.7.6'
	liquibaseRuntime 'org.liquibase:liquibase-core'
    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate6:4.30.0'
    liquibaseRuntime 'org.postgresql:postgresql'
    liquibaseRuntime 'org.springframework.boot:spring-boot'
    liquibaseRuntime 'org.springframework:spring-orm'
    liquibaseRuntime 'javax.xml.bind:jaxb-api'
    liquibaseRuntime 'org.hibernate:hibernate-core:6.6.2.Final'
    liquibaseRuntime sourceSets.main.output
}

diff.dependsOn compileJava
diffChangelog.dependsOn compileJava
generateChangelog.dependsOn compileJava

liquibase {
    activities {
        main {
			driver 'org.postgresql.Driver'
            url 'jdbc:postgresql://localhost:5432/bric48db'
            username 'dbuser'
            password 'password'
            changelogFile 'src/main/resources/db/changelog/generated-changelog.xml'
            referenceUrl 'hibernate:spring:it.infotechconsults.bric48.backend.domain' +
                    '?dialect=' +
                    'org.hibernate.dialect.PostgreSQLDialect'
        }
    }
}

hibernate {
  enhancement {
    enableAssociationManagement = true
  }
}

task composeUpDb {
	doLast {
		exec {
			executable "docker"
			args "compose", "-f", "src/main/docker/postgresql.yml", "up", "-d"
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
